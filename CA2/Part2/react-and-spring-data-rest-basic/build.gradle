plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.4'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'org.siouan.frontend-jdk17' version '8.0.0'
}

group = 'com.greglturnquist'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-rest'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    runtimeOnly 'com.h2database:h2'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
    useJUnitPlatform()
}

frontend {
    nodeVersion = '16.20.2'
    assembleScript = 'run build'
    cleanScript = 'run clean'
    checkScript = 'run check'
}

//task to copy the generated file Jar to a new package dist
task copyJarToDist(type: Copy) {
    //project jar is generated in build/libs directory
    from 'build/libs/'
    into 'dist'
    //includes all files that ends with .jar
    include '*.jar'
}

//task to delete the generated webpack files
task cleanWebpackFiles(type: Delete) {
    //the files generated by Webpack are referred to as "bundles"
    delete 'src/main/resources/static/built/'
}

clean.dependsOn cleanWebpackFiles



